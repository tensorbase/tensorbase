/*
 * Ref: https://github.com/antlr/grammars-v4/blob/master/sqlite/SQLite.g4
 * known problems: 
 * 1. now no support for LIKE and exists and analysis func...
 * 2. some complex nested exprs may be bad
 * 3. pest has some problems in backtracking
 */
cmd_list = { SOI ~ cmd+ ~ (";" ~ cmd)* ~ EOI }
cmd = {
    ^"explain"? ~ query ~ (";")* |
    create_database ~ (";")* |
    create_table ~ (";")* |
    show_databases ~ (";")* |
    show_tables ~ (";")* | 
    show_create_table ~ (";")* |
    desc_table ~ (";")* |
    drop_database ~ (";")* |
    drop_table ~ (";")* |
    truncate_table ~ (";")* |
    optimize_table ~ (";")* |
    insert_into ~ (";")* |
    use_db ~ (";")*
}

//--- ddl ---
create_database = {
    ^"create" ~ ^"database" ~ !^"if" ~ database_name |
    ^"create" ~ ^"database" ~ if_not_exists ~ database_name
}

create_table = {
    ^"create" ~ ^"table" ~ if_not_exists? ~ qualified_table_name ~ "(" ~ column_def ~ ("," ~ column_def)* ~ ")" ~ table_attributes?
}
if_not_exists = { ^"if" ~ ^"not" ~ ^"exists" }
column_def = {
    column_name ~ type_name ~ column_constraint?
}
column_constraint = {
    ^"primary" ~ ^"key" ~ sort_order? |
    ^"not" ~ ^"null" |
    ^"default" ~ literal
}
table_attributes = {
    table_attr_engine? ~ 
    table_attr_partition? ~
    table_attr_settings?
}
table_attr_engine = { ^"engine" ~ "=" ~ name }
table_attr_partition = { 
    ^"partition" ~ ^"by" ~ arith_expr  
}
table_attr_settings = { ^"settings" ~ attr_pair_list }
attr_pair_list = { attr_pair ~ ("," ~ attr_pair)* }
attr_pair = { name ~ "=" ~ signed_number }

drop_database = { ^"drop" ~ ^"database" ~ if_exists? ~ database_name }

drop_table = { ^"drop" ~ ^"table" ~ if_exists? ~ qualified_table_name }
if_exists = { ^"if" ~ ^"exists" }

truncate_table = { ^"truncate" ~ ^"table" ~ if_exists? ~ qualified_table_name }

optimize_table = { ^"optimize" ~ ^"table" ~ qualified_table_name }

//--- dml ---

show_databases = { ^"show" ~ ^"databases" }

show_tables = { ^"show" ~ ^"tables" }

show_create_table = { ^"show" ~ ^"create" ~ ^"table" ~ qualified_table_name }

desc_table = { (^"describe" | ^"desc") ~ ^"table"? ~ qualified_table_name }

//use_db
use_db = { ^"use" ~ database_name }

//insert
insert_into = { 
    ^"insert" ~ ^"into" ~ 
    qualified_table_name ~  
    column_list? ~ 
    format_clause? ~
    select?
 }
column_list = { "(" ~ column_name ~ ( "," ~ column_name )* ~ ")" }
format_clause = {
    ^"values" ~ rows* |
    ^"format" ~ ^"values" |
    ^"format" ~ format_name
}
format_name = { ^"csv" }
rows = { ( "(" ~ row ~ ")" ~ "," )* ~ "(" ~ row ~ ")" }
row = { ( literal ~ "," )* ~ literal }
// query
//TODO other kinds of dml
//FIXME limit and order_by not supported by subqueries?
query = { with? ~ compound_select }

with = { ^"with" ~ with_query ~ ("," ~ with_query)* }
with_query = { table_name ~ ^"as" ~ "(" ~ compound_select ~ ")" }

compound_select = {
    ( select | select_with_alias ) ~ (compound_op ~ ( select | select_with_alias ))*    
}
compound_op = { ^"union" ~ ^"all" | ^"union" | ^"except" | ^"intersect" }
select_with_alias = { "(" ~ select ~ ")" ~ as_qualif? }
select = {
     select_clause ~ from? ~ where_clause? ~ group_by? ~ order_by? ~ limit?
}
select_clause = { ^"select" ~ ^"distinct"? ~ select_column ~ ("," ~ select_column)* }
select_column_all = { "*" }
select_column = { expr ~ as_qualif? | select_column_all | table_name ~ "." ~ "*" }

//FIXME strictly need to exclude all reserved words from id/name
as_qualif = { 
    ^"as"? ~ 
    !(^"limit" | ^"order" | ^"where" | ^"from" | ^"and" | ^"group" | ^"left" | ^"on" | compound_op) ~ 
    name
}

host_subname = { ASCII_ALPHANUMERIC ~ ( ( ASCII_ALPHANUMERIC ~ HYPHEN ~ ASCII_ALPHANUMERIC ) | ASCII_ALPHANUMERIC )* }
domain = { ASCII_ALPHA_LOWER+ }
host_name = { (host_subname ~ ".")* ~ domain }

ipv4_lit = @{ ASCII_DIGIT{1,3} ~ ( "." ~ ASCII_DIGIT{1,3} ){3} }
ipv6_lit = @{
    ( ":" | ASCII_ALPHANUMERIC{1,4} ) ~ ":" ~ ( ipv4_lit | ASCII_ALPHANUMERIC{1,4} | ":" )*
}

from = { ^"from" ~ ( remote_func | join_tables | compound_select ) }

remote_func = {
    ^"remote" ~ "(" ~ remote_addresses ~ "," ~ remote_table ~ ("," ~ username_lit ~ "," ~ password_lit)? ~ ")"
}

remote_table = ${
  (remote_database_name ~ ".")? ~ remote_table_name
}

remote_database_name = @{ name }
remote_table_name = @{ name }
username = @{ ASCII_ALPHANUMERIC* }
password = @{ ASCII_ALPHANUMERIC* }

username_lit = {"'" ~ username ~ "'"}
password_lit = {"'" ~ password ~ "'"}

ip = {
   ipv4_lit | ("[" ~ ipv6_lit ~ "]")
}

remote_addresses = {
   "'" ~ remote_address ~ ("," ~ remote_address)* ~ "'"
 }

ip_address = { ip ~ (":" ~ port)? }
host_address = { host_name ~ (":" ~ port)? }

remote_address = {
  ip_address | host_address
}

port = @{
  ASCII_DIGIT+
}

join_tables = {
    join_table ~ (join_op ~ join_table ~ join_cond?)*
}
join_table = {
    "(" ~ compound_select ~ ")" ~ as_qualif? |
    qualified_table_name ~ as_qualif?
    // qualified_table_name ~ "(" ~ (expr ~ ("," ~ expr)*)? ~ ")" ~ as_qualif?
}
join_cond = {
    ^"on" ~ logical_expr |
    ^"using" ~ "(" ~ column_name ~ ("," ~ column_name)* ~ ")"
}
join_op = {
    "," |
    ^"join" |
    ^"natural"? ~ join_type ~ ^"join"
}
join_type = { ^"left" ~ ^"outer"? | ^"inner" | ^"cross" }

where_clause = { ^"where" ~ logical_expr }
group_by = { ^"group" ~ ^"by" ~ expr ~ ("," ~ expr)* ~ (^"having" ~ logical_expr)? }
order_by = { ^"order" ~ ^"by" ~ sorted_column ~ ("," ~ sorted_column)* }
limit = { ^"limit" ~ expr }

sort_order = { ^"asc" | ^"desc" }
sorted_column = { expr ~ sort_order? }

expr_item = _{ 
    literal | 
    cast_expr | 
    func_call_expr | 
    qualified_name |
    "(" ~ expr_item ~ ")" 
}

//FIXME just workaround for tpcds sql
func_arg = { case_expr | arith_expr }
func_call_expr = { 
    func_name ~ "(" ~ ")" |
    func_name ~ "(" ~ func_arg ~ ("," ~ func_arg)* ~ ")" 
}

arith_op = { "+" | "-" | "*" | "/" }
arith_expr_item = {
    "(" ~ arith_expr_item ~ ")" |
    expr_item ~ (arith_op ~ expr_item)*
}
arith_expr = {
    arith_expr_item ~ (arith_op ~ arith_expr_item)*
}

//FIXME * is ugly, just allow * for count?
count_tuple_expr = { ^"count" ~ "(" ~ "*" ~ ")" | ^"count" ~ "(" ~ "1" ~ ")" }
//FIXME ugly workaround?
count_distinct_expr = { ^"count" ~ "(" ~ ^"distinct" ~ qualified_name ~ ")" }

cast_expr = { ^"cast" ~ "(" ~ arith_expr ~ ^"as" ~ simple_type ~ ")" }

case_expr = {
    ^"case" ~ (^"when" ~ arith_expr ~ ^"then" ~ arith_expr)+ ~ 
      (^"else" ~ arith_expr)? ~ ^"end" |
    ^"case" ~ (^"when" ~ logical_expr ~ ^"then" ~ arith_expr)+ ~ 
      (^"else" ~ arith_expr)? ~ ^"end" |  
    ^"case" ~ id ~ 
      (^"when" ~ arith_expr ~ ^"then" ~ arith_expr)+ ~ 
      (^"else" ~ arith_expr)? ~ ^"end" |
    ^"case" ~ id ~ 
      (^"when" ~ logical_expr ~ ^"then" ~ arith_expr)+ ~ 
      (^"else" ~ arith_expr)? ~ ^"end"    
}

expr = {
    count_tuple_expr |
    count_distinct_expr |
    case_expr |
    arith_expr    
}

literal = {
    signed_number |
    string_literal |
    blob |
    ^"null"
}

comp_op   = { "<>" | "<=" | ">=" | "!=" | "=" | "<" | ">" }
in_expr = { 
    expr ~ ^"in" ~ const_array | 
    expr ~ ^"in" ~ compound_select 
}
between_expr = { 
    expr ~ ^"between" ~ ( compound_select|expr ) ~ 
           ^"and" ~ ( compound_select|expr ) 
}

comp_expr_is_null = { expr ~ ^"is" ~ ^"null" }
comp_expr_is_not_null = { expr ~  ^"is" ~ ^"not" ~ ^"null" }
comp_expr_cmp_operand = { compound_select|expr }
comp_expr_cmp = { comp_expr_cmp_operand ~ comp_op ~ comp_expr_cmp_operand }

comp_expr = { 
    in_expr | 
    between_expr | 
    comp_expr_is_null |
    comp_expr_is_not_null |
    comp_expr_cmp |
    func_call_expr |
    qualified_name
}

les_or_op = { !(^"order") ~ ^"or" }
les_and_op = { ^"and" }
les_not_op = { ^"not" }

not_logical_val = { les_not_op ~ comp_expr }
logical_val  = { 
    not_logical_val |
    comp_expr |
    "(" ~ logical_val ~ ")"
}
or_logical_val = { les_or_op ~ logical_val }
and_logical_val = { les_and_op ~ logical_val }
or_logical_expr = {
    logical_val ~  ( or_logical_val )* |
    "(" ~ logical_val ~  ( or_logical_val )+ ~ ")"
}
and_logical_expr = { 
    logical_val ~  ( and_logical_val )* | 
    "(" ~ logical_val ~  ( and_logical_val )+ ~ ")"
}

and_logical_exprs_with_or = { 
    and_logical_expr ~ ( les_or_op ~ and_logical_expr )*
}

or_logical_exprs_with_and = { 
    or_logical_expr ~ ( les_and_op ~ or_logical_expr )*
}

logical_expr = {
    and_logical_exprs_with_or |
    or_logical_exprs_with_and |
    "(" ~ logical_expr ~ ")"
}

//--- basic type ---
id = @{
    ('A'..'Z' | "_" | 'a'..'z') ~ ("$" | '0'..'9' | 'A'..'Z' | "_" | 'a'..'z')* |
    // A keyword in double-quotes is an identifier.
    "\"" ~ ("\"\"" | !"\"" ~ ANY)* ~ "\"" |
    // A keyword enclosed in grave accents (ASCII code 96) is allowed as an identifier as well.
    "`" ~ ("``" | !"`" ~ ANY)* ~ "`"
}

// A keyword in single quotes is a string literal.
string_literal = @{ "'" ~ ("''" | !"'" ~ ANY)* ~ "'" }
blob = @{ ("x" | "X") ~ "'" ~ (hex_digit)+ ~ "'" }

number = _{ float | int }
int = @{ digit+ | ("0x" | "0X") ~ hex_digit+ }
float = @{
    digit+ ~ "." ~ digit* ~ exponent? |
    "." ~ digit+ ~ exponent? |
    digit+ ~ exponent
}
exponent = _{ ("e" | "E") ~ ("+" | "-")? ~ digit+ }
digit = _{ '0'..'9' }
hex_digit = _{ '0'..'9' | 'a'..'f' | 'A'..'F' }

//FIXME allow arithmetic and func call in array
const_num_array = { "(" ~ signed_number ~ ("," ~ signed_number)* ~ ")" }
const_str_array = { "(" ~ string_literal ~ ("," ~ string_literal)* ~ ")" }
const_array = _{ const_num_array | const_str_array }

name = _{ id } //FIXME 
database_name = { name }
table_name = { table_name_numbers | name }
table_name_numbers = {
    "numbers" ~ "(" ~ int ~ "," ~ int ~ ")" | 
    "numbers" ~ "(" ~ int ~ ")" | 
    "numbers"
}
qualified_table_name = ${ (database_name ~ ".")? ~ table_name }
column_name = { name }
func_name = { name }
qualified_name = ${ (name ~ ".")? ~ name }

type_name = {
    nullable_type |
    datetime_type |
    simple_type |
    decimal_type |
    fixed_string_type |
    low_cardinality_string_type |
    low_cardinality_tinytext_type
}
simple_type = {
    "UInt8" | "UInt16" | "UInt32" | "UInt64" | "UInt256" | 
    "Int8"  | "Int16"  | "Int32"  | "Int64"  | "Int128" | "Int256" |
    "Float32" | "Float64" | "UUID" | "String" | "Date"
}
fixed_string_type = {
    "FixedString" ~ "(" ~ number ~ ")"
}
datetime_type = {
    ("DateTime64" | "DateTime") ~ ("(" ~ string_literal ~ ")")?
}
decimal_type = {
    "Decimal" ~ "(" ~ number ~ "," ~ number ~ ")" |
    "Decimal32" ~ "(" ~ number ~ ")" |
    "Decimal64" ~ "(" ~ number ~ ")" |
    "Decimal128" ~ "(" ~ number ~ ")" |
    "Decimal256" ~ "(" ~ number ~ ")"
}
nullable_type = { "Nullable" ~ "(" ~ nullable_types ~ ")" }
nullable_types = {
    decimal_type | simple_type | datetime_type
}
low_cardinality_string_type = { "LowCardinality" ~ "(" ~ "String" ~ ")" }
low_cardinality_tinytext_type = { "LowCardinality" ~ "(" ~ "TinyText" ~ ")" }

signed_number = @{
    ("+" | "-")? ~ number
}

//FIXME EOI here?
COMMENT = _{
    // line comment
    "--" ~ ((!("\n") ~ ANY)* ~ ("\n" | EOI)) |
    // block comment
    "/*" ~ ((!"*/" ~ ANY)*)
}
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

/*
reserved_word = @{
    ^"add" | ^"all" | ^"alter" | ^"and" | ^"as" | ^"autoincrement" |
    ^"between" |
    ^"check" | ^"collate" | ^"commit" | ^"constraint" | ^"create" | ^"cross" |
    ^"default" | ^"deferrable" | ^"delete" | ^"distinct" | ^"drop" |
    ^"escape" | ^"exists" |
    ^"foreign" | ^"from" | ^"full" |
    ^"group" |
    ^"having" |
    ^"in" | ^"index" | ^"inner" | ^"insert" | ^"intersect" | ^"into" | ^"is" | ^"isnull" |
    ^"join" |
    ^"left" | ^"limit" |
    ^"natural" | ^"not" | ^"notnull" | ^"null" |
    ^"on" | ^"or" | ^"order" | ^"outer" |
    ^"primary" |
    ^"references" | ^"right" |
    ^"select" | ^"set" |
    ^"table" | ^"temporary" | ^"then" | ^"to" | ^"transaction" |
    ^"unique" | ^"update" | ^"using" |
    ^"values" |
    ^"when" | ^"where" |
    ^"recursive"
}
*/
